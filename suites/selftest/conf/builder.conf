# Copyright (C) 2019-2022 OKTET Labs Ltd. All rights reserved.
# SPDX-License-Identifier: Apache-2.0

TE_PLATFORM([], [], [-D_GNU_SOURCE], [], [],
            [logger_core tools conf_oid asn ndn \
             logic_expr ipc bsapi loggerten rcfapi  \
             confapi comm_net_engine rcfunix trc tapi rpcxdr    \
             rcfrpc rpc_types tapi_rpc tapi_tad  \
             tapi_env tapi_job tapi_tool tapi_dns tapi_fio \
             log_proc \
            ])

TE_TOOLS([rgt trc])
TE_TOOL_PARMS([trc], [--with-popups --with-log-urls])

TE_LIB_PARMS([rpcxdr], [], [],
             [--with-rpcdefs=tarpc_job.x.m4])

TE_LIB_PARMS([tapi_tad], [], [],
             [--with-atm --with-eth --with-ipstack --with-socket])

. $TE_BASE/engine/builder/te_functions
. $TE_BASE/engine/builder/te_ta_ssh_helper

define([TE_AGENT_BUILD], [
    if test -n "${$1}" -o -n "${$1_BUILD_HOST}" -o "${$1_BUILD}" = local ; then
        if ! te_is_in_list "${$1_TA_TYPE}" $TE_BS_PLATFORMS ; then
            te_ta_type_as_platform "$1"

            case "${$1_TA_TYPE}" in
                *aarch64*) TE_TA_HOST=aarch64-pc-linux ;;
                *64*) TE_TA_HOST=x86_64-pc-linux ;;
                *)    TE_TA_HOST=i386-pc-linux   ;;
            esac

            TE_AGENT_BUILD_PARMS=
            TE_AGENT_BUILD_CC=${$1_CC}
            TE_AGENT_BUILD_CFLAGS=${$1_CFLAGS}
            test -n "$TE_AGENT_BUILD_CC" && TE_AGENT_BUILD_CC="CC=$TE_AGENT_BUILD_CC"

            TE_PLATFORM_BUILD([${$1_TA_TYPE}], [])
            TE_PLATFORM([${$1_TA_TYPE}], [$TE_AGENT_BUILD_CC],
                        [$TE_AGENT_BUILD_CFLAGS -D_GNU_SOURCE],
                        [-D_GNU_SOURCE], [],
                        [logger_core tools conf_oid asn ndn comm_net_agent \
                         loggerta agentlib rcfpch tad netconf rpcxdr \
                         rpctransport rpc_types rpcserver ta_job rpcs_job],
                        [${$1_BUILD}])

            TE_LIB_PARMS([rpcxdr], [${$1_TA_TYPE}], [],
                         [--with-rpcdefs=tarpc_job.x.m4])

            TE_LIB_PARMS([tad], [${$1_TA_TYPE}], [],
                         [--with-atm --with-eth --with-ipstack --with-socket])

            TE_TA_TYPE([${$1_TA_TYPE}], [${$1_TA_TYPE}], [unix],
                       [--with-cfg-unix-daemons='nginx' --enable-pci],
                       [], [], [], [comm_net_agent])

            TE_TA_APP([ta_rpcprovider], [${$1_TA_TYPE}], [${$1_TA_TYPE}],
                      [ta_rpcprovider], [], [],
                      [ta_job rpcs_job rpcserver agentlib rpcxdrta \
                       rpc_types rpctransport loggerta tools logger_core],
                      [\${EXT_SOURCES}/build.sh --extra-deps='libpcre'],
                      [ta_rpcs], [])
        fi
    fi
])

TE_AGENT_BUILD([TE_IUT])
TE_AGENT_BUILD([TE_TST1])
